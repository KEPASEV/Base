
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТДокТЧ
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Комплектующие.Комплектующее, ВТДокТЧ.Номенклатура) КАК Комплектующее,
	|	ВЫБОР
	|		КОГДА Комплектующие.Комплектующее ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьКомплектующее,
	|	ЕСТЬNULL(Комплектующие.Количество, 0) * ВТДокТЧ.Количество КАК Количество
	|ПОМЕСТИТЬ ВТИтоговаяТаблица
	|ИЗ
	|	ВТДокТЧ КАК ВТДокТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Комплектующие КАК Комплектующие
	|		ПО ВТДокТЧ.Номенклатура = Комплектующие.Стеллаж
	|ГДЕ
	|	ВТДокТЧ.Номенклатура.ЭтоСтеллаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТДокТЧ.Номенклатура,
	|	ИСТИНА,
	|	ВТДокТЧ.Количество
	|ИЗ
	|	ВТДокТЧ КАК ВТДокТЧ
	|ГДЕ
	|	НЕ ВТДокТЧ.Номенклатура.ЭтоСтеллаж
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТИтоговаяТаблица.Комплектующее,
	|	СУММА(ВТИтоговаяТаблица.Количество) КАК Количество,
	|	ВТИтоговаяТаблица.ЕстьКомплектующее,
	|	ВТИтоговаяТаблица.Комплектующее.Представление КАК КомплектующееПред
	|ИЗ
	|	ВТИтоговаяТаблица КАК ВТИтоговаяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТИтоговаяТаблица.Комплектующее,
	|	ВТИтоговаяТаблица.ЕстьКомплектующее,
	|	ВТИтоговаяТаблица.Комплектующее.Представление";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если Не ВыборкаДетальныеЗаписи.ЕстьКомплектующее Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заданы комплектующие для стеллажа " + ВыборкаДетальныеЗаписи.КомплектующееПред;
			Сообщение.Сообщить();
		КонецЕсли;
		Если Не Отказ Тогда
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Комплектующее;
			Движение.Склад = Склад;
			Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
		КонецЕсли;
	КонецЦикла;
	
	Если Не Отказ Тогда
		Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
		Движения.Записать();
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		Запрос.Текст =  		
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток,
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Комплектующее,
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК КомплектующееПред
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МоментВремени,
		|			Склад = &Склад
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ.Комплектующее
		|					ИЗ
		|						ВТИтоговаяТаблица КАК ВТ)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
		
		Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени()));
		Запрос.УстановитьПараметр("Склад", Склад);
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает товара " + ВыборкаДетальныеЗаписи.КомплектующееПред;
			Сообщение.Сообщить();
		КонецЦикла;	
	КонецЕсли;
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
